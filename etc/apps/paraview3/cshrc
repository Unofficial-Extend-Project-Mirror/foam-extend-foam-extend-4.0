#----------------------------------*-sh-*--------------------------------------
# =========                 |
# \\      /  F ield         | foam-extend: Open Source CFD
#  \\    /   O peration     | Version:     4.0
#   \\  /    A nd           | Web:         http://www.foam-extend.org
#    \\/     M anipulation  | For copyright notice see file Copyright
#------------------------------------------------------------------------------
# License
#     This file is part of foam-extend.
#
#     foam-extend is free software: you can redistribute it and/or modify it
#     under the terms of the GNU General Public License as published by the
#     Free Software Foundation, either version 3 of the License, or (at your
#     option) any later version.
#
#     foam-extend is distributed in the hope that it will be useful, but
#     WITHOUT ANY WARRANTY; without even the implied warranty of
#     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#     General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with foam-extend.  If not, see <http://www.gnu.org/licenses/>.
#
# Script
#     paraview3/cshrc
#
# Description
#     Setup file for paraview-3.x
#     Sourced from FOAM-*/etc/cshrc
#
# Note
#     The env. variable 'ParaView_DIR' is required for building plugins
#------------------------------------------------------------------------------

# determine the cmake to be used
unsetenv CMAKE_HOME
foreach cmake ( cmake-2.8.2 cmake-2.6.4 cmake-2.6.2 cmake-2.4.6 )
    set cmake=$WM_THIRD_PARTY_DIR/$cmake/platforms/$WM_ARCH
    if ( -r $cmake ) then
        setenv CMAKE_HOME $cmake
        set path=($CMAKE_HOME/bin $path)
        break
    endif
end

if ( ! $?ParaView_MAJOR ) setenv ParaView_MAJOR paraview-3.8
if ( ! $?ParaView_VERSION ) setenv ParaView_VERSION 3.8.1

setenv ParaView_INST_DIR $WM_THIRD_PARTY_DIR/ParaView-$ParaView_VERSION
setenv ParaView_DIR $ParaView_INST_DIR/platforms/$WM_ARCH$WM_COMPILER

# add in python libraries if required
set paraviewPython=$ParaView_DIR/Utilities/VTKPythonWrapping
if ( -r $paraviewPython ) then
    if ($?PYTHONPATH) then
        setenv PYTHONPATH ${PYTHONPATH}:${paraviewPython}:$ParaView_DIR/lib/${ParaView_MAJOR}
    else
        setenv PYTHONPATH ${paraviewPython}:$ParaView_DIR/lib/${ParaView_MAJOR}
    endif
endif

if ( -r $ParaView_INST_DIR ) then
    set path=($ParaView_DIR/bin $path)
    setenv PV_PLUGIN_PATH $FOAM_LIBBIN
endif

unset cmake paraviewPython
# -----------------------------------------------------------------------------
