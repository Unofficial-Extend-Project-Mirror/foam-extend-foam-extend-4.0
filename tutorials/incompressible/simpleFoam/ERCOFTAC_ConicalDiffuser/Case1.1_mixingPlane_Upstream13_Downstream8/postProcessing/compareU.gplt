#
# Automatic post processing by Hkan Nilsson, Maryse Page and Martin Beaudoin
#
# $0: 1st argument, position
# $1: 2st argument, angle of diffuser in degrees
# $2: 3st argument, radius at cross-section
# $3: 4rd argument, case name of solution 1
# $4: 5th argument, time of sample for solution 1
#

pause 0 "arg0 : $0"
pause 0 "arg1 : $1"
pause 0 "arg2 : $2"
pause 0 "arg3 : $3"
pause 0 "arg4 : $4"
pause 0 "arg5 : $5"

angle = $1*3.141592654/180.  # angle of diffuser in radians
U0    = 11.6                 # axial mean velocity [m/s]

set output 'U$0.png'
set terminal png medium
#set output 'U$0.eps'
#set term postscript color  # color
#set term postscript        # black and white
#Enhanced Metafile Format, for vectorized MS Win32 plots:
#set terminal emf monochrome dashed 'Times' 20
#set output 'U$0.emf'

#
# ERCOFTAC TESTCASE 3: Swirling boundary layer in conical diffuser
#
# U/(U0) as a function of y (m), at corresponding sections
#

set autoscale
set noarrow
set nolabel
set nogrid
set grid

set xlabel 'y (mm)'
set ylabel 'U/(U_0)'
set title 'Section z = $0mm'
set xrange [0:220]
set xtics 0,20,220
set yrange [-0.2:1.3]
set ytics -0.2,0.1,1.3

#
plot \
     "../../measurements/test60/Mm$0.dat" \
     using ($$1):($$2/U0) title "Measured U" with points linewidth 2 \
     , \
     "../../measurements/test60/Mm$0.dat" \
     using ($$1):($$3/U0) title "Measured W" with points linewidth 2 \
     , \
     "../../Case1.1/sets/$4/Comp$0_Y_U.xy" \
     using (($2-$$1)*1000/cos(angle)):(($$4*cos(angle)+$$3*sin(angle))/U0)\
     title "Case1.1, U" with lines linewidth 2 \
     , \
     "../../Case1.1/sets/$4/Comp$0_Y_U.xy" \
     using (($2-$$1)*1000/cos(angle)):(-$$2/U0) \
     title "Case1.1, W" with lines linewidth 2 \
     , \
     "../sets/$4/Comp$0_Y_U.xy" \
     using (($2-$$1)*1000/cos(angle)):(($$4*cos(angle)+$$3*sin(angle))/U0)\
     title "$3, U" with lines linewidth 2 \
     , \
     "../sets/$4/Comp$0_Y_U.xy" \
     using (($2-$$1)*1000/cos(angle)):(-$$2/U0) \
     title "$3, W" with lines linewidth 2 \
