#
# Automatic post processing by Hï¿½an Nilsson, Maryse Page and Martin Beaudoin
#
# 1.0.1 2008-03-14 Maryse Page, Hydro-Quebec
#                  Correction for the k experimental values: ($$2/100) instead of ($$2/U0/U0)
#
# $0: 1st argument, position
# $1: 2st argument, angle of diffuser in degrees
# $2: 3st argument, radius at cross-section
# $3: 4rd argument, case name of solution 1
# $4: 5th argument, time of sample for solution 1
#

pause 0 "arg0 : $0"
pause 0 "arg1 : $1"
pause 0 "arg2 : $2"
pause 0 "arg3 : $3"
pause 0 "arg4 : $4"
pause 0 "arg5 : $5"

angle = $1*3.141592654/180.  # angle of diffuser in radians
U0    = 11.6                 # axial mean velocity [m/s]

set output 'k$0.png'
set terminal png medium
#set output 'k$0.eps'
#set term postscript color  # color
#set term postscript        # black and white
#Enhanced Metafile Format, for vectorized MS Win32 plots:
#set terminal emf monochrome dashed 'Times' 20
#set output 'k$0.emf'

#
# ERCOFTAC TESTCASE 3: Swirling boundary layer in conical diffuser
#
# k/(U0^2) as a function of y (m), at corresponding sections
#

set autoscale
set noarrow
set nolabel
set nogrid
set grid

set xlabel 'y (mm)'
set ylabel 'k/(U_0^2)'
set title 'Section z = $0mm'
set xrange [0:220]
set xtics 0,20,220
set yrange [0:0.020]
set ytics 0,0.0025,0.020

#
plot \
     "../../measurements/test60/k$0.dat" \
     using ($$1):($$2/100) title "Measured k" \
     ,\
     "../../Case1.1/sets/$4/Comp$0_Y_p_k_epsilon.xy" \
     using (($2-$$1)*1000/cos(angle)):($$3/U0/U0) \
     title "Case1.1, k" with lines linewidth 2 \
     ,\
     "../sets/$4/Comp$0_Y_p_k_epsilon.xy" \
     using (($2-$$1)*1000/cos(angle)):($$3/U0/U0) \
     title "$3, k" with lines linewidth 2 \
